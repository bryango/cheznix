From 2c3613456b534658810b90d8f329e97ab19600b1 Mon Sep 17 00:00:00 2001
From: Bryan Lai <bryanlais@gmail.com>
Date: Fri, 16 May 2025 16:34:30 +0800
Subject: [PATCH] geminicommit: init at 0.2.7

See: https://github.com/tfkhdyt/geminicommit
---
 pkgs/by-name/ge/geminicommit/package.nix | 49 ++++++++++++++++++++++++
 1 file changed, 49 insertions(+)
 create mode 100644 pkgs/by-name/ge/geminicommit/package.nix

diff --git a/pkgs/by-name/ge/geminicommit/package.nix b/pkgs/by-name/ge/geminicommit/package.nix
new file mode 100644
index 00000000000000..c22a5d0fc0b4ce
--- /dev/null
+++ b/pkgs/by-name/ge/geminicommit/package.nix
@@ -0,0 +1,49 @@
+{
+  lib,
+  buildGoModule,
+  fetchFromGitHub,
+  installShellFiles,
+  stdenv,
+}:
+
+buildGoModule (finalAttrs: {
+  pname = "geminicommit";
+  version = "0.2.7";
+
+  src = fetchFromGitHub {
+    owner = "tfkhdyt";
+    repo = "geminicommit";
+    tag = "v${finalAttrs.version}";
+    hash = "sha256-dWJzS9vfEEvfPA5IS5WZDwoNMaXXNuVIb3p2xZvSbbQ=";
+  };
+
+  vendorHash = "sha256-+eKJLXgKuUHelUjD8MpMa+cRP+clmYK+1olcb/jmabk=";
+
+  nativeBuildInputs = [
+    installShellFiles
+  ];
+
+  postInstall =
+    let
+      cmd = finalAttrs.meta.mainProgram;
+    in
+    lib.optionalString (with stdenv; buildPlatform.canExecute hostPlatform) ''
+      # `geminicommit` requires write permissions to $HOME for its `config.toml`
+      # ... which is automatically initiated on startup
+      export HOME=$(mktemp -d)
+
+      for shell in bash zsh fish; do
+        installShellCompletion \
+          --cmd "${cmd}" \
+          --"$shell" <($out/bin/"${cmd}" completion "$shell")
+      done
+    '';
+
+  meta = {
+    description = "CLI that generates git commit messages with Google Gemini AI";
+    homepage = "https://github.com/tfkhdyt/geminicommit";
+    license = lib.licenses.gpl3Only;
+    maintainers = with lib.maintainers; [ bryango ];
+    mainProgram = "geminicommit";
+  };
+})
